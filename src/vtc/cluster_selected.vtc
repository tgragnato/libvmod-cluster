varnishtest "test .cluster_selected"

varnish v1 -vcl {
    import cluster;
    import directors;

    backend s1 { .host = "${bad_backend}";}
    backend s2 { .host = "${bad_backend}";}
    backend s3 { .host = "${bad_backend}";}

    sub vcl_init {
	new rr = directors.round_robin();
	rr.add_backend(s1);
	rr.add_backend(s2);

	new real = directors.round_robin();
	real.add_backend(s3);

	new cl = cluster.cluster(rr.backend(), deny=s2, real=real.backend());
    }
    sub vcl_backend_fetch {
	set bereq.http.c1 = cl.cluster_selected();
	set bereq.http.b1 = bereq.backend;
	set bereq.http.c2 = cl.cluster_selected();
	set bereq.http.b2 = bereq.backend;
    }
    sub vcl_backend_error {
	set beresp.status = 200;
	set beresp.http.c1 = bereq.http.c1;
	set beresp.http.b1 = bereq.http.b1;
	set beresp.http.c2 = bereq.http.c2;
	set beresp.http.b2 = bereq.http.b2;
    }
} -start

client c1 {
	txreq
	rxresp
	expect resp.status == 200
	expect resp.http.c1 == true
	expect resp.http.b1 == s1
	expect resp.http.c2 == false
	expect resp.http.b2 == real
} -run
